@REM MySQL 5.6 initialize datadir
@ECHO OFF

REM Set the following variables before invoke this script (run setenv.bat):
REM TEST_DIR Directory where this test is located
REM MYSQL_HOME MySQL basedir
REM MYSQL_DATADIR MySQL datadir
REM MYSQL_DEFAULTS_FILE my.ini to be used by mysqld.exe and mysql.exe
REM MYSQL_PORT Port where mysqld will listen
REM MYSQL_PID_FILE Process Id file name to be generated by MySQL
REM MYSQL_LOG_ERROR Error Log file name. In MySQL 5.6 you cannot disable writing some information to the Windows Event Log.
REM MYSQL_SECURE_FILE_PRIV MySQL Directory from which you can load files using LOAD DATA INFILE
REM MYSQL56_BOOTSTRAPED_DATADIR Directory with MySQL empty data files

IF [%TEST_DIR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_HOME%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_DATADIR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_DEFAULTS_FILE%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_PORT%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_PID_FILE%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_LOG_ERROR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_SECURE_FILE_PRIV%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL56_BOOTSTRAPED_DATADIR%] == [] GOTO :ERROR_NO_VAR_SET

ECHO Checking if MySQL datadir is not initialized yet...
IF EXIST "%MYSQL_DATADIR%\mysql" GOTO :ERROR_MYSQL_DATADIR_ALREADY_INITIALIZED

ECHO Copying bootstraped datadir...
XCOPY /S %MYSQL56_BOOTSTRAPED_DATADIR% %MYSQL_DATADIR%\
IF NOT EXIST %MYSQL_SECURE_FILE_PRIV% MD %MYSQL_SECURE_FILE_PRIV%
ECHO . > %MYSQL_LOG_ERROR%

ECHO Starting MySQL 5.6 for the first time...
CALL %TEST_DIR%\support-files\mysql56-start.bat 10 30
IF %ERRORLEVEL% NEQ 0 GOTO :ERROR_MYSQL_START_FIRST_TIME

ECHO Shuting down MySQL 5.6 after datadir initialization...
CALL %TEST_DIR%\support-files\mysql-stop.bat
IF %ERRORLEVEL% NEQ 0 GOTO :ERROR_MYSQL_STOP_FIRST_TIME

ECHO MySQL 5.6 datadir initialization done!
GOTO :EOF


:ERROR_NO_VAR_SET
ECHO Environment variables are not set. Please run setenv.bat to set environment variables.
EXIT /B 1

:ERROR_MYSQL_DATADIR_ALREADY_INITIALIZED
ECHO MySQL Database alredy exists. MySQL datadir already initialized? Nothing to do.
EXIT /B 2

:ERROR_MYSQL_START_FIRST_TIME
ECHO Could not initialize datadir. Error starting MySQL for the first time. Check error log and Windows Event Log.
EXIT /B 3

:ERROR_MYSQL_STOP_FIRST_TIME
ECHO Error stopping MySQL for the first time. Check error log %MYSQL_LOG_ERROR% for details.
EXIT /B 4

:EOF
