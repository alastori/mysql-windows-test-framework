@REM MySQL 5.7 startup
@ECHO OFF

REM Usage: mysql57-start.bat <nr retires to start> <wait>

SET MYSQL_START_RETRIES=%1
SET MYSQL_START_WAIT=%2
IF [%MYSQL_START_RETRIES%] == [] SET MYSQL_START_RETRIES=0
IF [%MYSQL_START_WAIT%] == [] SET MYSQL_START_WAIT=0

REM Set the following variables before invoke this script (run setenv.bat):
REM TEST_DIR Directory where this test is located
REM MYSQL_HOME MySQL basedir
REM MYSQL_DATADIR MySQL datadir
REM MYSQL_DEFAULTS_FILE my.ini to be used by mysqld.exe and mysql.exe
REM MYSQL_PORT Port where mysqld will listen
REM MYSQL_PID_FILE Process Id file name to be generated by MySQL
REM MYSQL_LOG_ERROR Error Log file name. In MySQL 5.7 you can disable writing some information to the Windows Event Log using --log-syslog=0
REM MYSQL_SECURE_FILE_PRIV MySQL Directory from which you can load files using LOAD DATA INFILE

IF [%TEST_DIR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_HOME%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_DATADIR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_DEFAULTS_FILE%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_PORT%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_PID_FILE%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_LOG_ERROR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_SECURE_FILE_PRIV%] == [] GOTO :ERROR_NO_VAR_SET

ECHO Checking if MySQL datadir is initialized...
IF NOT EXIST "%MYSQL_DATADIR%\mysql" GOTO :ERROR_MYSQL_DATADIR_NOT_INITIALIZED

ECHO Checking if MySQL is already started...
CALL %TEST_DIR%\support-files\mysql-isdown.bat 5 5
IF %ERRORLEVEL% NEQ 0 GOTO :ERROR_MYSQL_ALREADY_RUNNING

ECHO Starting MySQL 5.7...
IF NOT EXIST %MYSQL_HOME%\bin\mysqld.exe GOTO :ERROR_MYSQL_NOT_FOUND
START "MySQL 5.7 mysqld.exe" /D %MYSQL_HOME% CMD /K "%MYSQL_HOME%\bin\mysqld.exe --defaults-file=%MYSQL_DEFAULTS_FILE% --basedir=%MYSQL_HOME% --port=%MYSQL_PORT% --datadir=%MYSQL_DATADIR% --pid-file=%MYSQL_PID_FILE% --secure-file-priv=%MYSQL_SECURE_FILE_PRIV% --log-error=%MYSQL_LOG_ERROR% --log-syslog=0 & EXIT"
TIMEOUT 5
CALL %TEST_DIR%\support-files\monitor-file.bat "MySQL 5.7 error log" %MYSQL_LOG_ERROR%
CALL %TEST_DIR%\support-files\mysql-isup.bat %MYSQL_START_RETRIES% %MYSQL_START_WAIT%
IF %ERRORLEVEL% NEQ 0 GOTO :ERROR_MYSQL_NOT_RUNNING
ECHO MySQL started.
GOTO :EOF

:ERROR_NO_VAR_SET
ECHO ERROR: Environment variables are not set. Please run setenv.bat to set environment variables.
EXIT /B 1

:ERROR_MYSQL_NOT_RUNNING
ECHO ERROR: Could not start and access MySQL.
EXIT /B 2

:ERROR_MYSQL_ALREADY_RUNNING
ECHO ERROR: There was another MySQL instance running. Could not start a new instance.
EXIT /B 3

:ERROR_MYSQL_DATADIR_NOT_INITIALIZED
ECHO ERROR: MySQL Database does not exist. Please initialize MySQL datadir first.
EXIT /B 4

:ERROR_MYSQL_NOT_FOUND
ECHO ERROR: MySQL Server (mysqld.exe) not found in %MYSQL_HOME%\bin. Please check variable MYSQL_HOME.
EXIT /B 5

:EOF
