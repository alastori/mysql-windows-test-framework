@REM MySQL 5.7 initialize datadir
@ECHO OFF

REM Set the following variables before invoke this script (run setenv.bat):
REM TEST_DIR Directory where this test is located
REM MYSQL_HOME MySQL basedir
REM MYSQL_DATADIR MySQL datadir
REM MYSQL_DEFAULTS_FILE my.ini to be used by mysqld.exe and mysql.exe
REM MYSQL_PORT Port where mysqld will listen
REM MYSQL_PID_FILE Process Id file name to be generated by MySQL
REM MYSQL_LOG_ERROR Error Log file name. In MySQL 5.6 you cannot disable writing some information to the Windows Event Log.
REM MYSQL_SECURE_FILE_PRIV MySQL Directory from which you can load files using LOAD DATA INFILE

IF [%TEST_DIR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_HOME%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_DATADIR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_DEFAULTS_FILE%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_PORT%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_PID_FILE%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_LOG_ERROR%] == [] GOTO :ERROR_NO_VAR_SET
IF [%MYSQL_SECURE_FILE_PRIV%] == [] GOTO :ERROR_NO_VAR_SET

ECHO Checking if MySQL datadir is not initialized yet...
IF EXIST "%MYSQL_DATADIR%\mysql" GOTO :ERROR_MYSQL_DATADIR_ALREADY_INITIALIZED

ECHO Checking if there is an MySQL already running...
CALL %TEST_DIR%\support-files\mysql-isdown.bat 5 5
IF %ERRORLEVEL% NEQ 0 GOTO :ERROR_MYSQL_ALREADY_RUNNING

ECHO Initializing MySQL 5.7 datadir...
IF NOT EXIST %MYSQL_SECURE_FILE_PRIV% MD %MYSQL_SECURE_FILE_PRIV%
SET MYSQL_LOG_ERROR_INIT=%TEST_DIR%\mysqld57.err
ECHO . > %MYSQL_LOG_ERROR_INIT%
CALL %TEST_DIR%\support-files\monitor-file.bat "MySQL 5.7 initial error log" %MYSQL_LOG_ERROR_INIT%
%MYSQL_HOME%\bin\mysqld.exe --defaults-file=%MYSQL_DEFAULTS_FILE% --initialize-insecure --basedir=%MYSQL_HOME% --port=%MYSQL_PORT% --datadir=%MYSQL_DATADIR% --pid-file=%MYSQL_PID_FILE% --secure-file-priv=%MYSQL_SECURE_FILE_PRIV% --log-error=%MYSQL_LOG_ERROR_INIT% --log-syslog=0
IF %ERRORLEVEL% NEQ 0 GOTO :ERROR_MYSQL_INITIALIZATION

ECHO Starting MySQL 5.7 for the first time...
CALL %TEST_DIR%\support-files\mysql57-start.bat
IF %ERRORLEVEL% NEQ 0 GOTO :ERROR_MYSQL_START_FIRST_TIME

ECHO Shuting down MySQL 5.7 after datadir initialization...
CALL %TEST_DIR%\support-files\mysql-stop.bat
IF %ERRORLEVEL% NEQ 0 GOTO :ERROR_MYSQL_STOP_FIRST_TIME

ECHO MySQL 5.7 datadir initialization done!
GOTO :EOF


:ERROR_NO_VAR_SET
ECHO Environment variables are not set. Please run setenv.bat to set environment variables.
EXIT /B 1

:ERROR_MYSQL_DATADIR_ALREADY_INITIALIZED
ECHO MySQL Database alredy exists. MySQL datadir already initialized? Nothing to do.
EXIT /B 2

:ERROR_MYSQL_ALREADY_RUNNING
ECHO There was another MySQL instance running. Could not start a new instance.
EXIT /B 3

:ERROR_MYSQL_INITIALIZATION
ECHO Error during MySQL initialization. Check error log %MYSQL_LOG_ERROR_INIT% for details.
EXIT /B 4

:ERROR_MYSQL_START_FIRST_TIME
ECHO Error starting MySQL for the first time. Check error log %MYSQL_LOG_ERROR_INIT% for details.
EXIT /B 5

:ERROR_MYSQL_STOP_FIRST_TIME
ECHO Error stopping MySQL for the first time. Check error log %MYSQL_LOG_ERROR_INIT% for details.
EXIT /B 6

:EOF
